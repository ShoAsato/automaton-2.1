module record1 where

record _∧_ (A B : Set) : Set where
  field
     π1 : A
     π2 : B






ex1 : {A B : Set} → ( A ∧ B ) → A 
ex1 a∧b =  _∧_.π1 a∧b

open _∧_

ex0' : {A B : Set} → A ∧ B →  A
ex0' =  π1 

ex1' : {A B : Set} → ( A ∧ B ) → A 
ex1' a∧b =  π1 a∧b



-- ex2 証明図
--       A ∧ B
--  --------------- π2
--         B
--  ---------------
--   ( A ∧ B ) → B 

ex2 : {A B : Set} → ( A ∧ B ) → B 
ex2 a∧b = π2 a∧b

-- ex3 証明図
--       A   B
--  ----------------- intro
--       A ∧ B
--  -----------------
--  A → B → ( A ∧ B )

ex3 : {A B : Set} → A → B → ( A ∧ B ) 
ex3 a b = record { π1 = a ;  π2 = b }


-- ex4 証明図
--       A   A
--  ----------------- intro
--       A ∧ A
--  -----------------
--  A → A → ( A ∧ A )

ex4 : {A B : Set} → A → ( A ∧ A ) 
ex4 a  = record { π1 = a ;  π2 = a }

--ex5 証明図
--            A ∧ B ∧ C
--                :
--            A ∧ B ∧ C
--   ------------------------------- λ-intro
--    ( A ∧ B ) ∧ C  →  A ∧ (B ∧ C)

ex5 : {A B C : Set} → ( A ∧ B ) ∧ C  →  A ∧ (B ∧ C) 
ex5 a∧b∧c  = record { π1 = π1 (π1 a∧b∧c); π2 = record { π1 = π2 (π1 a∧b∧c) ;  π2 = π2 a∧b∧c}}


--
--                                 [(A → B ) ∧ ( B →  C) ]₁  
--                                ──────────────────────────────────── π1
--     [(A → B ) ∧ ( B →  C) ]₁        (A → B )    [A]₂
--   ──────────────────────────── π2 ─────────────────────── λ-elim
--          ( B →  C)                     B
--   ─────────────────────────────────────────── λ-elim
--                   C
--   ─────────────────────────────────────────── λ-intro₂
--                 A → C
--   ─────────────────────────────────────────── λ-intro₁
--     ( (A → B ) ∧ ( B →  C) )  → A → C


ex6 : {A B C : Set} → ( (A → B ) ∧ ( B →  C) )  → A → C
ex6 x a = π2 x (π1 x a)

--ex6 証明図
--  A → B   B →  C
-- ---------------- λ-elim
--    A → B → C   A → B
-- ---------------------- λ-elim
--          C
--                A → C
--  --------------------------------
--   (A → B ) → ( B →  C)  → A → C


ex6' : {A B C : Set} →  (A → B ) → ( B →  C)   → A → C
ex6' a b c = b (a c)
